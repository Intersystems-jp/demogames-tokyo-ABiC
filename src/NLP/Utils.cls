Class NLP.Utils
{

/// ãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ã®ãƒ™ãƒ¼ã‚¹URL
/// Parameter baseurl = "https://localhost:9443/csp/user/_DeepSee.UserPortal.DashboardViewer.zen?DASHBOARD=";
Parameter baseurl = "https://ec2-52-194-192-117.ap-northeast-1.compute.amazonaws.com/csp/user/_DeepSee.UserPortal.DashboardViewer.zen?DASHBOARD=";

/// Word,Column,MDXColumn,TableName,Cube,Expression
ClassMethod loadcsv()
{
    &sql(
        LOAD DATA FROM FILE '/opt/src/data/NLP/metainfo.csv'
        INTO NLP.MetaInfo (Word,Column,MDXColumn,TableName,CubeName,Expression)
        USING {"from":{"file":{"charset":"UTF-8","header":true}}}
    )
}

/// ãƒãƒ«ãƒãƒªãƒ³ã‚¬ãƒ«ãƒ¢ãƒ‡ãƒ«ç”¨
ClassMethod Vector()
{
    set select="select ID,Word From NLP.MetaInfo"
    set stmt1=##class(%SQL.Statement).%New()
    set st=stmt1.%Prepare(select)
    set rset1=stmt1.%Execute()
    set stmt2=##class(%SQL.Statement).%New()
    // ãƒãƒ«ãƒãƒªãƒ³ã‚¬ãƒ«ãƒ¢ãƒ‡ãƒ«
    set st=stmt2.%Prepare("UPDATE NLP.MetaInfo set WordVec=(TO_VECTOR(EMBEDDING(?,'my-sentenceTransformer-config'),DOUBLE,384)) WHERE ID=?")
    while rset1.%Next() {
        set rset2=stmt2.%Execute(rset1.Word,rset1.ID)
        if rset2.%SQLCODE<0 {
            write rset2.%Message,!
        }
    }
}

/// å…¥ã‚Œå¿˜ã‚ŒãŸã¨ã
ClassMethod WordAdd(w As %String, c As %String = "", mdx As %String, t As %String = "", cube As %String, expression As %String(MAXLEN=500))
{
    set sql="insert into NLP.MetaInfo (Word,Column,MDXColumn,TableName,Cube,Expression,WordVec) VALUES(?,?,?,?,?,?,TO_VECTOR(EMBEDDING(?,'my-sentenceTransformer-config'),DOUBLE,384))"
    set stmt=##class(%SQL.Statement).%New()
    set st=stmt.%Prepare(sql)
    if $$$ISERR(st) {
        write $SYSTEM.Status.GetErrorText(sql),!
        return
    }
    set rset=stmt.%Execute(w,c,mdx,t,cube,expression,w)
    if rset.%SQLCODE<0 {
        write rset.%Message,!
    }
}

/// Wordã®ä¸­èº«ãŒé•ã†ãªãƒ»ãƒ»ã¨æ€ã£ãŸã¨ãã®ä¿®æ­£ãƒ­ã‚¸ãƒƒã‚¯ï¼ˆãƒãƒ«ãƒãƒªãƒ³ã‚¬ãƒ«ãƒ¢ãƒ‡ãƒ«ç”¨ï¼‰
ClassMethod WordUpdate(word As %String, new As %String)
{
    set stmt=##class(%SQL.Statement).%New()
    set status=stmt.%Prepare("select ID from NLP.MetaInfo where Word=?")
    set rset=stmt.%Execute(word)
    do rset.%Next()
    set id=rset.ID
    &sql(start transaction)
    &sql(UPDATE NLP.MetaInfo SET Word=:new WHERE ID=:id)
    if SQLCODE<0 {
        write %msg,!
        &sql(rollback)
        return
    }
    // VectoråŒ–
    &sql(
        UPDATE NLP.MetaInfo
         set WordVec=(TO_VECTOR(EMBEDDING(:new,'my-sentenceTransformer-config'),DOUBLE,384))WHERE ID=:id
    )
    if SQLCODE<0 {
        write %msg,!
        &sql(rollback)
        write "æ›´æ–°ã¯ãªã‹ã£ãŸã“ã¨ã«",!
        return
    }
    write "ã†ã¾ãã„ãã¾ã—ãŸ",!
    &sql(commit)
}

/// ãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ä½œæˆ
/// ãƒ”ãƒœãƒƒãƒˆãƒ†ãƒ¼ãƒ–ãƒ«è¡¨ç¤ºã¨ã™ã‚‹ã€‚DateOfExãŒã‚ã‚‹å ´åˆã¯ãƒ•ã‚£ãƒ«ã‚¿ã«Yearã¨Monthã‚’ç½®ãã‚ˆã†ã«ã™ã‚‹
/// pivots ãƒ”ãƒœãƒƒãƒˆåã‚’ç¬¬1ã‚µãƒ–ã‚¹ã‚¯ãƒªãƒ—ãƒˆã«å…¥ã‚Œã¦æ¸¡ã™
///     pivots("t1")=""
///     pivots("t2")=""
/// dash ãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰å
/// åŒåã®ãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ãŒã‚ã‚‹å ´åˆã¯å‰Šé™¤ã—ã¦ã‹ã‚‰ä½œæˆã™ã‚‹
/// Yearã®ãƒ•ã‚£ãƒ«ã‚¿ã®å€¤ã‚’æŒ‡å®šã§ãã‚‹ã‚ˆã†ã«ã—ãŸï¼š&[2024]
/// Monthã®ãƒ•ã‚£ãƒ«ã‚¿ã®å€¤ã‚’æŒ‡å®šï¼š&[12] ãªã©
ClassMethod createDashboard(ByRef pivots As %String, dash As %String, DateOfEx As %Boolean = 0, Dept As %Boolean = 0, YearFilt As %String = "", MonthFilt As %String = "", Listing As %Boolean = 0) As %Status
{
    set status=$$$OK
    try {
        if ##class(%DeepSee.Dashboard.Utils).%DashboardExists(dash_".dashboard")'=0 {
            set flg=##class(%DeepSee.Dashboard.Utils).%DeleteDashboard(dash_".dashboard")
            if flg=0 {
                throw ##class(%Exception.General).%New("Dashboradå‰Šé™¤ã‚¨ãƒ©ãƒ¼",5001,,dash_"ã‚’å‰Šé™¤ã§ãã¾ã›ã‚“ã§ã—ãŸ")
            }
        }
        // ãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ä½œæˆ
        set d=##class(%DeepSee.Dashboard.Definition).%New()
        // ãƒ¯ãƒ¼ã‚¯ãƒªã‚¹ãƒˆã®è¨­å®šï¼ˆãƒ•ã‚£ãƒ«ã‚¿ãŒãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ã«ã¤ã„ã¦ã‚‹ã®ã§ä»˜ã‘ã‚‹æ™‚ã¯1ã«ï¼‰
        set d.worklistCount=0
        set d.createdBy="SuperUser"
        set d.name=dash
        /* snapGridã¨snapToãªã„ã¨è¡¨ç¤ºã•ã‚Œãªã„*/
        set d.snapGrid=1
        set d.snapTo=1
        //widgetsç™»éŒ²
        set pname="",num=1,(homeR,homeC)=0,colS=5,rowS=5
        for {
            set pname=$ORDER(pivots(pname),1,cubename)
            if pname="" quit
            set w=##class(%DeepSee.Dashboard.Widget).%New()
            set w.name=pname
            set w.type="pivot"
            set w.subtype="pivot"
            set w.title=pname
            set w.subtypeClass="lineChart"
            set w.width=300
            set w.height=300
            // ä½•å€‹ç›®ã‹ã®ãƒ”ãƒœãƒƒãƒˆã§å ´æ‰€ã‚’å¤‰æ›´
            if num>1 {
                //2ã§å‰²ã£ã¦ä½™ã‚Š1ã®æ™‚                 
                if num#2 {
                    set homeR=homeR+rowS
                    set homeC=0
                    set colS=5
                }
                else {
                    set homeR=homeR
                    set rowS=5
                    set homeC=homeC+colS
                }
            }
            set w.homeRowL=homeR
            set w.homeColL=homeC
            set w.colSpanL=colS
            set w.rowSpanL=rowS
            if cubename[".kpi" {
                set w.dataSource=cubename
                set w.name="StockInfo"  //æ¸¡ã£ã¦ãã‚‹ã®ã¯"kpipivot"ãªã®ã§KPIã¯StockInfoã¨ã™ã‚‹
                // ã‚¦ã‚£ã‚¸ã‚§ãƒƒãƒˆã«ãƒ•ã‚£ãƒ«ã‚¿ã‚’ä»˜ã‘ã‚‹
                set c1=##class(%DeepSee.Dashboard.Control).%New()
                set c1.action="applyFilter"
                set c1.label="Year"
                set c1.location="widget"
                set c1.targetProperty="Year"
                do w.controls.Insert(c1)
                set c2=##class(%DeepSee.Dashboard.Control).%New()
                set c2.action="applyFilter"
                set c2.label="Month"
                set c2.location="widget"
                set c2.targetProperty="Month"
                do w.controls.Insert(c2)
                set c3=##class(%DeepSee.Dashboard.Control).%New()
                set c3.action="ShowAreaInfo"
                set c3.label="ShowAreaInfo"
                set c3.location="widget"
                set c3.type="auto"
                do w.controls.Insert(c3)
            }
            else {
                set w.dataSource=pname_".pivot"
                do w.properties.SetAt("chart","chartToggle")
            }
            //controlsç™»éŒ²
            if (num=1)&&(DateOfEx=1) {
                set c1=##class(%DeepSee.Dashboard.Control).%New()
                set c2=##class(%DeepSee.Dashboard.Control).%New()
                set c1.action="applyFilter"
                set c2.action="applyFilter"
                set c1.targetProperty="[DateOfEx].[H1].[Year]"
                set c1.label="Year"
                set c2.targetProperty="[DateOfEx].[H1].[Month]"
                set c2.label="Month"
                set c1.location="dashboard"
                set c2.location="dashboard"
                set c1.target="*"
                set c2.target="*"
                set c1.value=YearFilt
                set c2.value=MonthFilt
                do w.controls.Insert(c1)
                do w.controls.Insert(c2)
            }
            if (num=1)&&(Dept=1) {
                set c3=##class(%DeepSee.Dashboard.Control).%New()
                set c3.action="applyFilter"
                set c3.targetProperty="[Department].[H1].[Department]"
                set c3.label="Department"
                set c3.location="dashboard"
                set c3.target="*"
                do w.controls.Insert(c3)
            }
            // è©³ç´°ãƒªã‚¹ãƒˆã‚’ä»˜ã‘ã‚‹å ´åˆ
            //Cubeåã‹ã‚‰è©³ç´°ãƒªã‚¹ãƒˆã‚’å–å¾—ã—ã€ã‚ã‚‹åˆ†ã ã‘ã‚¦ã‚£ã‚¸ã‚§ãƒƒãƒˆã«ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ«ã¨ã—ã¦è¨­å®š
            if (Listing=1)&&(cubename'[".kpi") {
                $$$ThrowOnError(##class(%DeepSee.Utils).%GetCubeListings(cubename,.listing))
                set lcn=""
                for {
                    set lcn=$Order(listing(lcn))
                    if lcn="" quit
                    set clisting=##class(%DeepSee.Dashboard.Control).%New()
                    set clisting.action="showListing"
                    set clisting.targetProperty=lcn
                    set clisting.label=lcn
                    set clisting.location="widget"
                    set clisting.type="auto"
                    set clisting.activeWhen="itemSelected"
                    do w.controls.Insert(clisting)
                }
            }
            kill cubename
            do d.widgets.Insert(w)
            set num=num+1
        }
        $$$ThrowOnError(d.%Save())
    }
    catch ex {
        set status=ex.AsStatus()
    }
    return status
}

/// ã€Šãƒ”ãƒœãƒƒãƒˆä½œæˆæ–¹æ³•ã€‹
/// %DeepSee.Dashboard.Pivotã®ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ç”Ÿæˆ
///  cubeNameã«ã‚­ãƒ¥ãƒ¼ãƒ–åè¨­å®š
///  name ã«ãƒ”ãƒœãƒƒãƒˆåæŒ‡å®šï¼ˆæ‹¡å¼µå­ã„ã‚‰ãªã„ï¼‰ 
///  rowLevelsã«ãƒ‡ã‚£ãƒ¡ãƒ³ã‚¸ãƒ§ãƒ³ã®å†…å®¹å…¥ã£ã¦ã‚‹ï¼ˆãƒªã‚¹ãƒˆï¼‰
/// ã€€ã€€rowLevelsã®ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ã¯ %DeepSee.Dashboard.PivotLevel ã®ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹
///     ã€€textã€€ã«ãƒ¬ãƒ™ãƒ«è¡¨ç¤ºå
///     ã€€spec ã«MDXå¼
///     CROSSJOINã—ã¦ã‚‹å ´åˆã¯childLevelsã‚’ä½¿ã†
///         childLevelsãƒ—ãƒ­ãƒ‘ãƒ†ã‚£ã‚‚ãƒªã‚¹ãƒˆã€ã‚¿ã‚¤ãƒ—ã¯%DeepSee.Dashboard.PivotLevel
///         textã«ãƒ¬ãƒ™ãƒ«è¡¨ç¤ºå
///         specã«MDXå¼
///     do rowLevels.Insert(ä½œã£ãŸã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹)ã€€ã§è¨­å®šã™ã‚‹
///  Measureã¯Pivotã‚¯ãƒ©ã‚¹ã®measuresï¼ˆãƒªã‚¹ãƒˆï¼‰%DeepSee.Dashboard.PivotLevel
///     textã«è¡¨ç¤ºå
///     specã«MDXå¼
/// ==============================================================
/// ç¬¬ï¼‘ä½å¼•æ•°ï¼šãƒ‡ã‚£ãƒ¡ãƒ³ã‚¸ãƒ§ãƒ³
///     æ·»ãˆå­—ã«MDXï¼ˆ"row or col",[xx].Members ãªã©ï¼‰ ãƒ‡ãƒ¼ã‚¿éƒ¨ã¯è¡¨ç¤ºå
///     ç¬¬ï¼‘ãƒ¬ãƒ™ãƒ«ã®ã¿ã®å ´åˆ
///         dimentions("row","[xx].Members")="è¡¨ç¤ºå"  ->è¡ŒæŒ‡å®š
///         dimentions("col","[xxx].Members")="è¡¨ç¤ºå"  -> åˆ—æŒ‡å®š
///     å­ãƒ¬ãƒ™ãƒ«ã‚’æŒã¤å ´åˆ
///         dimentions("row","[xx].Members","[childxx].Members")="è¡¨ç¤ºå"
///     æ·»ãˆå­—ã®éšå±¤é€šã‚Šã«å­ãƒ¬ãƒ™ãƒ«ã¨ã—ã¦è¨­å®šã™ã‚‹
/// ç¬¬2å¼•æ•°ï¼šãƒ¡ã‚¸ãƒ£ãƒ¼ã®ã¿
///     measure("[xx]")="è¡¨ç¤ºå"
/// ç¬¬3å¼•æ•°ï¼šã‚­ãƒ¥ãƒ¼ãƒ–å
/// ç¬¬4å¼•æ•°ï¼šä½œæˆã™ã‚‹ãƒ”ãƒœãƒƒãƒˆåï¼ˆæ—¢å­˜åã‚ã£ã¦ã‚‚ä¿å­˜å¯èƒ½ã ã‘ã©æ—¢å­˜æ®‹ã‚‹ï¼‰
ClassMethod createPivot(ByRef dimentions As %String, ByRef measure As %String, cube As %String, pivotname As %String = "try9") As %Status
{
    set status=$$$OK
    try {
        //åŒã˜ãƒ”ãƒœãƒƒãƒˆåãŒå­˜åœ¨ã—ã¦ã‚‹ã‹ãƒã‚§ãƒƒã‚¯ã—ã€å­˜åœ¨ã—ã¦ã‚Œã°å‰Šé™¤
        do ..deleteSameNamePivot(pivotname)
        set p=##class(%DeepSee.Dashboard.Pivot).%New()
        set p.createdBy="SuperUser"
        set p.name=pivotname
        set p.cubeName=cube
        //ãƒ‡ã‚£ãƒ¡ãƒ³ã‚¸ãƒ§ãƒ³è¨­å®š
        set leveltype=""
        for {
            set leveltype=$ORDER(dimentions(leveltype))
            if leveltype="" quit
            do ..SetLevel(leveltype,.dimentions,.p)
        }
        //ãƒ¡ã‚¸ãƒ£ãƒ¼è¨­å®š
        set mmdx=""
        for {
            set mmdx=$ORDER(measure(mmdx),1,data)
            if mmdx="" quit
            set m=##class(%DeepSee.Dashboard.PivotLevel).%New()
            set m.text=data
            set m.spec=mmdx
            do p.measures.Insert(m)
            kill data
        }
        $$$ThrowOnError(p.%Save())
    }
    catch ex {
        set status=ex.AsStatus()
    }
    return status
}

/// name : ãƒ”ãƒœãƒƒãƒˆåï¼ˆæ‹¡å¼µå­ãªã—ï¼‰
ClassMethod deleteSameNamePivot(name As %String) As %Status
{
    set status=$$$OK
    try {
        set sql="select ID FROM %DeepSee_Dashboard.Pivot where name=?"
        set stmt=##class(%SQL.Statement).%New()
        $$$ThrowOnError(stmt.%Prepare(sql))
        set rset=stmt.%Execute(name)
        while rset.%Next() {
            set id=rset.ID
            &sql(delete from %DeepSee_Dashboard.Pivot where ID=:id)
            if SQLCODE<0 {
                throw ##class(%Exception.SQL).CreateFromSQLCODE(SQLCODE,%msg)
            }
        }
    }
    catch ex {
        set status=ex.AsStatus()
    }
    return status
}

ClassMethod SetLevel(leveltype As %String, ByRef dimentions As %String, ByRef pivot As %DeepSee.Dashboard.Pivot)
{
    set r1=""
    for {
        set r1=$ORDER(dimentions(leveltype,r1),1,data1)
        if r1="" quit
        // %DeepSee.Dashboard.PivotLevelã®ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ç”Ÿæˆ
        set level1=##class(%DeepSee.Dashboard.PivotLevel).%New()
        set level1.spec=r1
        set level1.text=data1
        //å­ãƒ¬ãƒ™ãƒ«ã‚ã‚‹ã‹ã©ã†ã‹ ï¼ˆã¨ã‚Šã‚ãˆãšï¼’ãƒ¬ãƒ™ãƒ«ã¾ã§ã«ã—ã¦ãŠãï¼‰
        set r2=""
        for {
            set r2=$ORDER(dimentions(leveltype,r1,r2),1,data2)
            if r2="" quit
            set level2=##class(%DeepSee.Dashboard.PivotLevel).%New()
            set level2.spec=r2
            set level2.text=data2
            do level1.childLevels.Insert(level2)
            kill data2
        }
        //pivotã«è¨­å®š
        if leveltype="row" {
            do pivot.rowLevels.Insert(level1)
        }
        else {
            do pivot.columnLevels.Insert(level1)
        }
        kill data1
    }
}

ClassMethod testPivot(name As %String = "t1")
{
    set d("row","[NightShiftFlag].[H1].[NightShiftFlag].Members")="NightShiftFlag"
    set d("row","[NightShiftFlag].[H1].[NightShiftFlag].Members","[Department].[H1].[Department].Members")="Department"
    set d("col","[EmpRole].[H1].[EmpRole].Members")="EmpRole"
    set m("[Measures].[%COUNT]")="Count"
    set cube="ABiCAttendanceCube"
    set pivotname=name
    set st=..createPivot(.d,.m,cube,pivotname)
    if $$$ISERR(st) {
        write $system.Status.GetErrorText(st),!
    }
}

ClassMethod Test1(a As %String) As %String [ SqlName = Test1, SqlProc ]
{
    set ^iijima("d")={}.%FromJSON(a)
    set ^iijima("m")=a
    return "ãŠã£ã‘ãƒ¼"
}

/// in: è‡ªç„¶è¨€èªå…¥åŠ›ã‹ã‚‰å–ã‚Šå‡ºã—ãŸã‚³ãƒ³ã‚»ãƒ—ãƒˆã®JSONæ–‡å­—åˆ—ï¼ˆé…åˆ—ï¼‰
/// æˆ»ã‚Šå€¤ï¼šçµæœã®å…¥ã£ãŸãƒ€ã‚¤ãƒŠãƒŸãƒƒã‚¯ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆ
/// {"url":"ãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ã®URL","recommend":"ãƒ¬ã‚³ãƒ¡ãƒ³ãƒ‰æ–‡å­—åˆ—"}
/// 
/// ã‚»ãƒƒãƒˆä¾‹
/// set moji="[""number"", ""patients"", ""average age"", ""department"", ""examination"", ""gender""]"
/// set moji="[""number"", ""incident reports"", ""fault level"", ""continuity"", ""number"", ""incident reports"", ""april"", ""2024""]"
/// set moji="[""number"",""employees"", ""role"", ""working nights"", ""days"", ""april"", ""2024""]"
/// æˆ»ã‚Šï¼šæ–‡å­—ã‚’è¿”ã™ï¼ˆã‚¨ãƒ©ãƒ¼ã§ã‚‚æ–‡å­—ï¼‰
ClassMethod GetAnswer(in As %String(MAXLEN="")) As %String
{
    set status=$$$OK
    set returnjson=""
    try {
        set start=$ZH
        set inputarray={}.%FromJSON(in)
        //ãƒãƒ«ãƒãƒªãƒ³ã‚¬ãƒ«ãƒ¢ãƒ‡ãƒ«
        //set query = "SELECT TOP 1 VECTOR_COSINE(WordVec,(TO_VECTOR(EMBEDDING(?,'my-sentenceTransformer-config'),DOUBLE,384))) as sim,ID,Column,MDXColumn,TableName,CubeName,Expression from NLP.MetaInfo Order By sim desc"
        //è»½é‡ãƒ¢ãƒ‡ãƒ«
        set query = "SELECT TOP 1 VECTOR_COSINE(WordVec,EMBEDDING(?,'smallmodel-sentence-transformers-config')) as sim,ID,Column,MDXColumn,TableName,CubeName,Expression from NLP.MetaInfo Order By sim desc"
        set stmt=##class(%SQL.Statement).%New()
        $$$ThrowOnError(stmt.%Prepare(query))
        set iter=inputarray.%GetIterator()
        // cube("å")=ã‚«ã‚¦ãƒ³ãƒˆ
        // dim("å¼")=""
        // measure("å¼")=""
        while iter.%GetNext(.key,.val) { 
            //w val,!
            if val="number" { continue }
            // ã‚‚ã—YYYYï¼ˆæ•°å€¤ã ã‘ï¼‰ãŒããŸã‚‰ãƒ•ã‚£ãƒ«ã‚¿ãƒ¼ã¨ã™ã‚‹
            set rset=stmt.%Execute(val)
            // TOP 1ãªã®ã§
            do rset.%Next()
            if rset.CubeName'="" {
                // ãƒ•ã‚£ãƒ«ã‚¿ã«ä½¿ã†ã‚‚ã®ã¯ã‚­ãƒ¥ãƒ¼ãƒ–åãŒãªã„
                set cube(rset.CubeName)=$Get(cube(rset.CubeName))+1
            }
            set expression=rset.Expression
            if expression'="" {
                set mflg=$PIECE(expression,".",$LENGTH(expression,"."))
                if mflg'="Members" {
                    if $EXTRACT(expression)="&" {
                        // YearFilter ä¾‹ã€€&[2024] å…¨éƒ¨ã§7æ–‡å­—
                        set:$LENGTH(expression)=7 yearfilt=expression
                        // MonthFilter ä¾‹ &[11] å…¨éƒ¨ã§5æ–‡å­—ä»¥ä¸‹
                        set:$LENGTH(expression)<7 monthfilt=expression
                    }
                    else {
                        set measure(expression)=rset.MDXColumn
                    }
                }
                else {
                    set dim(expression)=rset.MDXColumn
                }
            }
        }

        //************
        //set end=$ZH
        //write "Vector Serarchå®Ÿè¡Œæ™‚é–“ï¼š",end-start,!
        //************

        // CubeNameã®å¤šæ•°æ±º
        set n="",topcount=0,topcube=""
        for {
            set n=$ORDER(cube(n),1,count)
            if n="" quit
            if topcount<count {
                set topcount=count
                set topcube=n
            }
            kill count
        }

        // pivotä½œæˆ
        /*
            å¤‰æ•°dã«d("row",å¼)="MDXè¡¨ç¤ºå"
            å¤‰æ•°mã«m(ãƒ¡ã‚¸ãƒ£ãƒ¼ã®å¼)="MDXè¡¨ç¤ºå"
        */
        set dcn="",prev="",cn=1
        for {
            set dcn=$order(dim(dcn),1,dname)
            if dcn="" quit
            if cn=1 {
                set d("row",dcn)=dname
            }
            if cn=2 {
                set d("row",prev,dcn)=dname
            }
            if cn=3 {
                set d("col",dcn)=dname
            }
            if cn=4 {
                set d("col",prev,dcn)=dname
            }
            set prev=dcn
            set cn=cn+1
            // dnameã«ConsecutiveDayãŒã§ã¦ããŸã‚‰ABiCConsecutiveDayCubeã‚’ä½¿ã†ã“ã¨
            if dname="ConsecutiveDay" {
                set topcube="ABiCConsecutiveDayCube"
            }
        }

        //pivotä½œæˆ
        set num=$increment(^ABiC("pivotNum"))
        set pivotname="ABiCPivot"_num
        set ^ABiC("pivotName",num)=pivotname_"^"_topcube
        $$$ThrowOnError(##class(NLP.Utils).createPivot(.d,.measure,topcube,pivotname))
        
        //Dashboardä½œæˆ
        if '$data(^ABiC("dashName")) { 
            set num=$increment(^ABiC("DashNum"))
            set dashname="ABiCDash"_num
            set ^ABiC("dashName",num)=dashname
        }
        else {
            set dashname=^ABiC("dashName",$Get(^ABiC("DashNum")))
        }
        //ä½œæˆæ¸ˆpivotãŒã‚ã£ãŸã‚‰ãã‚Œã‚’è¿½åŠ ã™ã‚‹
        set exnum=""
        for {
            set exnum=$order(^ABiC("pivotName",exnum),1,expivot)
            if exnum="" quit
            set p($PIECE(expivot,"^",1))=$PIECE(expivot,"^",2)
        }
        //ä»Šå›ä½œæˆã—ãŸãƒ”ãƒœãƒƒãƒˆã‚’è¿½åŠ 
        // p(pivotname)=cubeå
        set p(pivotname)=topcube
        
        if $get(^ABiC("pivotNum"))=2 {
            // KPIè¿½åŠ ã™ã‚‹
            set p("kpipivot")="SQLBaseKPI.kpi"
            /*
            set num=$increment(^ABiC("pivotNum"))
            set ^ABiC("pivotName",num)="SQLBaseKPI.kpi"
            */
        }

        //************
        //zw p,dim,measure,d,monthfilt
        //************

        $$$ThrowOnError(##class(NLP.Utils).createDashboard(.p,dashname,1,1,$get(yearfilt),$get(monthfilt),1))

        //************
        //set end=$ZH
        //write "å®Ÿè¡Œæ™‚é–“ï¼š",end-start,!
        //************
        set url=..#baseurl_dashname_".dashboard"
        set j={}
        set j.url=url
        set j.explain="ğŸ’¡ After changing to pivot window, select a cell and click on the binoculars button to see the original data of the analysis results."
        set:$data(p("kpipivot")) j.recommend="ğŸ‘ Do you need to check your inventory data? I put the pivot in your dashboard."
        //set returnjson=j.%ToJSON()
        // Adaptive Cardä½œæˆ
        set facts1=##class(NLP.CardFacts).%New()
        set card=##class(NLP.Card).%New()
        set facts1.Key="How to use it: "
        set facts1.Value=j.explain
        do card.Facts.Insert(facts1)
        if j.recommend'="" {
            set facts2=##class(NLP.CardFacts).%New()
            set facts2.Key="Recommend: "
            set facts2.Value=j.recommend
            do card.Facts.Insert(facts2)
        } 
        set card.createDate=$ZDATE(+$H)
        set card.URL=url
        $$$ThrowOnError(card.OutputToDynamicObject(.returnJson))
        return returnJson.%ToJSON()
        //return j.%ToJSON()
   }
    catch ex {
        set status=ex.AsStatus()
        set url=$SYSTEM.Status.GetErrorText(status)
    }
    return returnjson
}

/// ã“ã‚Œæ¶ˆãˆãªã„
ClassMethod ABiCDelete()
{
    write "ä½œæˆã—ãŸãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ã¨ãƒ”ãƒœãƒƒãƒˆã‚’æ¶ˆã—ã¾ã™",!
    set cn=""
    for {
        set cn=$ORDER(^ABiC("dashName",cn),1,data)
        if cn="" quit
        set fullname=data_".dashboard.DFI"
        set status=##class(%DeepSee.Dashboard.Utils).%DeleteDashboard(fullname)
        if status=0 {
            write "æ¶ˆã›ãªã‹ã£ãŸæ§˜å­",!
            quit
        }
        kill data
    }
    set cn=""
    for {
        set cn=$ORDER(^ABiC("pivotName",cn),1,data)
        if cn="" quit
        set fullname=data_".pivot.DFI"
        set st=##class(NLP.Utils).deleteSameNamePivot(fullname)
        if $$$ISERR(st) {
            write $system.Status.GetErrorText(st),!
            quit
        }
        kill data
    }
    //kill ^ABiC
}

/// è¨ˆé‡ãƒ¢ãƒ‡ãƒ«ç”¨ãƒ†ã‚¹ãƒˆ
ClassMethod GetAnswerTest(in As %String(MAXLEN="")) As %String
{
    set status=$$$OK
    set returnjson=""
    try {
        set start=$ZH
        set inputarray={}.%FromJSON(in)
        set query = "SELECT TOP 1 VECTOR_COSINE(WordVec,EMBEDDING(?,'smallmodel-sentence-transformers-config')) as sim,ID,Column,MDXColumn,TableName,CubeName,Expression from NLP.MetaInfo2 Order By sim desc"
        set stmt=##class(%SQL.Statement).%New()
        $$$ThrowOnError(stmt.%Prepare(query))
        set iter=inputarray.%GetIterator()
        // cube("å")=ã‚«ã‚¦ãƒ³ãƒˆ
        // dim("å¼")=""
        // measure("å¼")=""
        while iter.%GetNext(.key,.val) { 
            //w val,!
            if val="number" { continue }
            // ã‚‚ã—YYYYï¼ˆæ•°å€¤ã ã‘ï¼‰ãŒããŸã‚‰ãƒ•ã‚£ãƒ«ã‚¿ãƒ¼ã¨ã™ã‚‹
            set rset=stmt.%Execute(val)
            // TOP 1ãªã®ã§
            do rset.%Next()
            if rset.CubeName'="" {
                // ãƒ•ã‚£ãƒ«ã‚¿ã«ä½¿ã†ã‚‚ã®ã¯ã‚­ãƒ¥ãƒ¼ãƒ–åãŒãªã„
                set cube(rset.CubeName)=$Get(cube(rset.CubeName))+1
            }
            set expression=rset.Expression
            if expression'="" {
                set mflg=$PIECE(expression,".",$LENGTH(expression,"."))
                if mflg'="Members" {
                    if $EXTRACT(expression)="&" {
                        // YearFilter ä¾‹ã€€&[2024] å…¨éƒ¨ã§7æ–‡å­—
                        set:$LENGTH(expression)=7 yearfilt=expression
                        // MonthFilter ä¾‹ &[11] å…¨éƒ¨ã§5æ–‡å­—ä»¥ä¸‹
                        set:$LENGTH(expression)<7 monthfilt=expression
                    }
                    else {
                        set measure(expression)=rset.MDXColumn
                    }
                }
                else {
                    set dim(expression)=rset.MDXColumn
                }
            }
        }

        //************
        set end=$ZH
        write "Vector Serarchå®Ÿè¡Œæ™‚é–“ï¼š",end-start,!
        //************
    }
    catch ex {
        set status=ex.AsStatus()
        set url=$SYSTEM.Status.GetErrorText(status)
    }
    return returnjson
}

}
